managed; //implementation in class zbp_i_shw_rap_travel unique;
//strict;
with draft;

define behavior for ZI_SHW_RAP_TRAVEL alias Travel
implementation in class ZCL_I_SHW_RAP_TRAVEL unique
persistent table ZSHW_RAP_TRAVEL
draft table ZSHW_RAP_TRAV_DR
lock master total etag LocalLastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  association _Booking { create; with draft; }

  field ( numbering : managed, readonly ) TravelUuid;
  mapping for ZSHW_RAP_TRAVEL
  {
      TravelUuid = travel_uuid;
      TravelId = travel_id;
      AgencyId = agency_id;
      CustomerId = customer_id;
      BeginDate = begin_date;
      EndDate = end_date;
      BookingFee = booking_fee;
      TotalPrice = total_price;
      CurrencyCode = currency_code;
      Description = description;
      TravelStatus = overall_status;
      CreatedBy = created_by;
      CreatedAt = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
  }

  field (readonly) TravelId, TotalPrice, TravelStatus;
  field (readonly) CreatedBy, CreatedAt, LastChangedAt, LastChangedBy, LocalLastChangedAt;
  field (mandatory) AgencyId, CustomerId;

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  internal action recalculateTotalPrice;

  determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination calculateTravelId on modify { create; }

  validation validateAgency on save { field AgencyId; create; }
  validation validateCustomer on save { field CustomerId; create; }
  validation validateDates on save { field BeginDate, EndDate; create; }

  draft determine action Prepare {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
  }

}

define behavior for ZI_SHW_RAP_BOOKING alias Booking
implementation in class ZCL_I_SHW_RAP_BOOKING unique
persistent table ZSHW_RAP_BOOKING
draft table ZSHW_RAP_BOOK_DR
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  association _Travel{ with draft; }

  field ( readonly ) TravelUuid, BookingId;
  field (readonly) CreatedBy, LastChangedBy, LocalLastChangedAt;
  field ( numbering : managed, readonly ) BookingUuid;

  determination calculateBookingId on modify { create; }
  determination calculateTotalPrice on modify { field FlightPrice, CurrencyCode; }


  mapping for zshw_rap_booking
  {
     BookingUuid = booking_uuid;
     TravelUuid =  travel_uuid;
     BookingId =  booking_id;
     BookingDate =  booking_date;
     CustomerId =  customer_id;
     CarrierId =  carrier_id;
     ConnectionId =  connection_id;
     FlightDate =  flight_date;
     FlightPrice =  flight_price;
     CurrencyCode =  currency_code;
     CreatedBy =  created_by;
     LastChangedBy =  last_changed_by;
     LocalLastChangedAt =  local_last_changed_at;
  }
}